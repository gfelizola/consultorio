<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009"
					xmlns:s="library://ns.adobe.com/flex/spark"
					xmlns:mx="library://ns.adobe.com/flex/mx"
					xmlns:view="app.view.*"
					width="1004" height="500" creationComplete="init()" show="init()">
	<fx:Script>
		<![CDATA[
			import app.controller.Navigation;
			import app.enums.EAreas;
			import app.model.AtividadeFisica;
			import app.model.Consulta;
			import app.model.DB;
			import app.model.Paciente;
			import app.skin.ComboBox;
			import app.util.General;
			import app.util.Helpers;
			
			import mx.collections.ArrayCollection;
			import mx.containers.ViewStack;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			
			import nz.co.codec.flexorm.criteria.Criteria;
			import nz.co.codec.flexorm.criteria.Sort;
			
			import spark.components.ComboBox;
			import spark.events.IndexChangeEvent;

			private var consultas:ArrayCollection;
			
			[Bindable]
			private var minIdade:Number;
			
			[Bindable]
			private var pesos:ArrayCollection;
			
			private function init():void
			{
				var p:Paciente = General.pacienteAtual ;
				if( p == null ) {
					Navigation.navega(EAreas.HOME);
					return ;
				}
				
				if( p.dataNascimento ){
					var idade:Number = Math.floor( Helpers.idade(p.dataNascimento) / 12 );
					txtIdade.text = idade + " ANOS" ;
				} else {
					txtIdade.visible = txtIdade.includeInLayout = false ;
				}
				
				var perfil:String = "";
				txtSemanaGestacional.visible = false ;
				
				if( idade > 60 ) {
					perfil = "IDOSO" ;
				} else if( idade < 20 ) {
					perfil = "CRIANÇA" ;
				} else {
					if( p.gestante )
					{
						perfil = "GESTANTE" ;
						if( p.dataUltimaMenstruacao ){
							txtSemanaGestacional.text = p.semanaGestacional + "ª SEMANA GESTACIONAL" ;
							txtSemanaGestacional.visible = true ;
						} else {
							txtSemanaGestacional.visible = txtSemanaGestacional.includeInLayout = false ;
						}
					} else {
						perfil = "ADULTO" ;
						if( p.nutriz ) perfil += " / NUTRIZ" ;
					}
				}
				
				txtPerfil.text = perfil ;
				
				var c:Criteria = DB.em.createCriteria(app.model.Consulta);
				c.addEqualsCondition("paciente", p.id );
				c.addSort("dataConsulta", Sort.DESC );
				
				consultas = DB.em.fetchCriteria(c);
				var consulta:app.model.Consulta = consultas.getItemAt(0) as app.model.Consulta;
				
				if( consulta ) {
					txtUltimaConsulta.text = Helpers.dateFormat(consulta.dataConsulta);
					
					if( consulta.antropometria ){
						txtUltimoIMC.text = consulta.antropometria.imc.toFixed(2) ;
					} else {
						txtUltimoIMC.text = "N/A" ;
					}
					
					if( consulta.atividadeFisica )
					{
						var nivelAtividade:String = "" ;
						switch(consulta.atividadeFisica.nivel)
						{
							case AtividadeFisica.MUITO_ATIVO:
								nivelAtividade = "MUITO ATIVO" ;
								break;
							case AtividadeFisica.ATIVO:
								nivelAtividade = "ATIVO" ;
								break;
							case AtividadeFisica.POUCO_ATIVO:
								nivelAtividade = "POUCO ATIVO" ;
								break;
							case AtividadeFisica.SEDENTARIO:
								nivelAtividade = "SEDENTÁRIO" ;
								break;
						}
						
						txtAtividade.text = nivelAtividade ;
					} else {
						txtAtividade.visible = txtAtividade.includeInLayout = false ;
					}
					
					if( idade < 20 ) {
						perfil = "CRIANÇA" ;
						vsGraficos.selectedIndex = 1 ;
						cmbTipoGrafico.selectedIndex = 2 ;
						trocaGrafico( GraficosCrescimento.TIPO_IMC );
					}
				}
				else
				{
					txtUltimaConsulta.text = "NENHUMA CONSULTA REALIZADA" ;
					txtUltimoIMC.text = "N/A" ;
					txtAtividade.text = "" ;
					txtAtividade.visible = txtAtividade.includeInLayout = false ;
					
					Alert.noLabel = "Não" ;
					Alert.yesLabel = "Sim" ;
					
					Alert.show("Este paciente ainda não possui nenhuma consulta cadastrada.\nDeseja iniciar uma agora", "Confirma consulta", Alert.YES|Alert.NO, null, onAlertaNovaConsulta);
				}
			}
			
			private function onAlertaNovaConsulta(e:CloseEvent):void
			{
				if( e.detail == Alert.YES )
				{
					if( General.consultaAtual == null ) General.consultaAtual = new app.model.Consulta();
					ViewStack(this.parent).selectedIndex = 3 ;
				}
			}
			
			private function trocaGrafico( tipo:int ):void
			{
				gpGraficos.removeAllElements();
				
				var gc:GraficosCrescimento = new GraficosCrescimento();
				gc.percentWidth = 100 ;
				gc.percentHeight = 100 ;
				gc.tipo = tipo ;
				gc.consultas = consultas ;
				
				gpGraficos.addElement( gc );
			}
			
			private function onComboGraficoChange(e:IndexChangeEvent):void
			{
				trocaGrafico( spark.components.ComboBox(e.currentTarget).selectedIndex ) ;
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:HGroup top="10" bottom="10" left="10" right="10" gap="20">
		<s:Group width="205" height="100%">
			<s:Rect width="100%" height="100%" radiusX="10" radiusY="10">
				<s:fill>
					<s:SolidColor color="#FAFDFE"></s:SolidColor>
				</s:fill>
			</s:Rect>
			
			<s:VGroup gap="6" x="10" y="10">
				<s:Label id="txtPerfil" width="100%" color="#81BAEA" fontSize="20" styleName="bold" text="ADULTO"/>
				<s:Label id="txtIdade" width="100%"  text="30 ANOS"/>
				<s:Label id="txtAtividade" width="100%"  text="SEDENTÁRIO"/>
				<s:Label id="txtSemanaGestacional" width="100%" text="32 SEMANA GESTACIONAL"/>
			</s:VGroup>
			
			<s:Label x="10" y="200" width="100%" height="30" color="#81BAEA" fontSize="20" styleName="bold" text="ÚLTIMA CONSULTA"/>
			<s:Label id="txtUltimaConsulta" x="10" y="230" width="100%" height="60" text="30 ANOS"/>
			
			<s:Label x="10" y="350" width="100%" height="30" color="#81BAEA" fontSize="20" styleName="bold" text="ÚLTIMO IMC"/>
			<s:Label id="txtUltimoIMC" x="10" y="380" width="100%" height="20" text="18,5"/>
			
			<mx:HRule x="10" y="170" width="185" height="2" chromeColor="#E0EFFB"/>
			<mx:HRule x="10" y="330" width="185" height="2" chromeColor="#E0EFFB"/>
		</s:Group>
		
		<s:Group width="100%" height="100%">
			<s:Rect width="100%" height="100%" radiusX="10" radiusY="10">
				<s:fill>
					<s:SolidColor color="#FAFDFE"></s:SolidColor>
				</s:fill>
			</s:Rect>
			
			<mx:ViewStack id="vsGraficos" top="10" bottom="10" left="10" right="10">
				<view:Graficos width="100%" height="100%" />
				
				<s:NavigatorContent width="100%" height="100%">
					<s:VGroup width="100%" height="100%">
						<s:ComboBox id="cmbTipoGrafico" width="300" skinClass="app.skin.ComboBox" change="onComboGraficoChange(event)">
							<s:dataProvider>
								<mx:ArrayList>
									<fx:String>PESO x IDADE</fx:String>
									<fx:String>ESTATURA x IDADE</fx:String>
									<fx:String>IMC x IDADE</fx:String>
									<fx:String>PESO x ESTATURA</fx:String>
								</mx:ArrayList>
							</s:dataProvider>
						</s:ComboBox>
						
						<s:VGroup id="gpGraficos" width="100%" height="100%">
						</s:VGroup>
					</s:VGroup>
					
				</s:NavigatorContent>
			</mx:ViewStack>
		</s:Group>	
	</s:HGroup>
</s:NavigatorContent>
